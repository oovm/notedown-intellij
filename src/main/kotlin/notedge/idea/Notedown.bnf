{
    parserClass = "notedge.idea.language.parser.NoteParser"
    parserUtilClass = "notedge.idea.language.psi.ParserExtension"

    extends = "notedge.idea.language.ast.NotedownASTBase"

    psiClassPrefix = "Note"
    psiImplClassSuffix = "Node"
    psiPackage = "notedge.idea.language.psi"
    psiImplPackage = "notedge.idea.language.psi_node"
    psiImplUtilClass = "notedge.idea.language.psi.NodeExtension"

    elementTypeHolderClass = "notedge.idea.language.psi.NoteTypes"
    elementTypeClass = "notedge.idea.language.psi.NElementType"
    tokenTypeClass = "notedge.idea.language.psi.NElementType"
    tokens = [
        NEW_LINE = 'NewLine'
        ESCAPE = '\'
        ACCENT = '^'
        EQ = '='
        COLON = ':'
        SEMICOLON = ';'
        COMMA = ','
        DOLLAR = '$'
        DOT = '.'
        STAR = '*'
        AT = '@'
    ]
}

Notedown ::= statement*;

private statement ::=
    header
  | function
  | xml
  | text_elements
  | code_block
  | BREAK_PART
  ;
// ---------------------------------------------------------------------------------------------------------------------
header ::= HEADER_HASH text_elements {
    mixin = "notedge.idea.language.mixin.MixinHeader"
}
// ---------------------------------------------------------------------------------------------------------------------
text_elements ::= text_item+;
private text_item ::= PLAIN_TEXT | escaped | ESCAPE | NEW_LINE
  | italic | bold | strong
  | under | strike | wave
  | snippet | math
escaped ::= ESCAPED_CHARACTER
// ---------------------------------------------------------------------------------------------------------------------
italic ::= ITALIC_L [text_elements] ITALIC_R // *text*
bold   ::= BOLD_L   [text_elements] BOLD_R   // **text**
strong ::= STRONG_L [text_elements] STRONG_R // ***text***
// ---------------------------------------------------------------------------------------------------------------------
under  ::= UNDER_L  [text_elements] UNDER_R  // _text_
wave   ::= WAVE_L   [text_elements] WAVE_R   // __text__
strike ::= STRIKE_L [text_elements] STRIKE_R // ___text___
// ---------------------------------------------------------------------------------------------------------------------
snippet ::= CODE_L string? CODE_R;
math    ::= MATH_L string? MATH_R // $math$
// ---------------------------------------------------------------------------------------------------------------------
code_block  ::= CODE_S string CODE_E;
// ---------------------------------------------------------------------------------------------------------------------
string ::= (STRING_TEXT | ESCAPE_TEXT)+;
// ---------------------------------------------------------------------------------------------------------------------
xml ::= xml_close | xml_start xml_end
xml_close ::= ANGLE_L  namespace ANGLE_SR  // <name.space arg kw=arg/>
xml_start ::= ANGLE_L  namespace ANGLE_R   // <name.space arg kw=arg>
xml_end   ::= ANGLE_SL namespace ANGLE_R   // </name.space>
// ---------------------------------------------------------------------------------------------------------------------
// \name.space
function ::= ESCAPE namespace [args_function] [args_text|args_block] {

}
args_function ::= <<parenthesis argument COMMA>>
args_text ::= COLON STRING_TEXT?
args_block ::= <<brace_block STRING_TEXT>>
argument ::= [key (EQ|COLON)] value
key ::= identifier
value ::= namespace
// ---------------------------------------------------------------------------------------------------------------------
list ::= <<bracket_block value COMMA>>
// ---------------------------------------------------------------------------------------------------------------------
identifier ::= SYMBOL
namespace ::= identifier (DOT identifier)*
// ---------------------------------------------------------------------------------------------------------------------
// parenthesis ( )
private meta parenthesis ::= PARENTHESIS_L [<<item>> (<<sp>> <<item>>)* [<<sp>>]] PARENTHESIS_R;
// bracket [ ]
meta bracket_block ::= BRACKET_L [<<item>> (<<sp>> <<item>>)* [<<sp>>]] BRACKET_R;
// brace { }
meta brace_block ::= BRACE_L <<item>>* BRACE_R;

